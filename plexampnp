#!/usr/bin/perl

# TODO: Add support for multiple sessions
#       Limit to only music sessions
#       Limit to only sessions with a certain user

use strict;
use warnings;
use open qw(:std :utf8);
use feature 'say';

use Getopt::Long;
Getopt::Long::Configure qw(bundling);

use XML::Simple;
use LWP::Simple;

my $PLEX_HOST = '192.168.1.34';
my $PLEX_PORT = '32400';
my $PLEX_SESS = '/status/sessions';

my $PLEX_URL = "http://$PLEX_HOST:$PLEX_PORT$PLEX_SESS";

GetOptions(
  'host=s'   => \$PLEX_HOST,
  'port=s'   => \$PLEX_PORT,
  'n|np'     => sub { say np() },
  'a|artist' => sub { say np('artist') },
  't|title'  => sub { say np('title') },
  'A|album'  => sub { say np('album') },
  'y|year'   => sub { say np('year') },
  'f|file'   => sub { say np('file') },

  'h|help'   => sub {
    say "Usage: $0 [options]";
    say "  --host\t\tSet the host (default: $PLEX_HOST)";
    say "  --port\t\tSet the port (default: $PLEX_PORT)";
    say "  -np\t\t\tPrint the full now playing string";
    say "  -a, --artist\t\tPrint the artist";
    say "  -t, --title\t\tPrint the title";
    say "  -A, --album\t\tPrint the album";
    say "  -f, --file\t\tPrint the full path to the file";
    say "  -y, --year\t\tPrint the year";

    say "  -h, --help\t\tThis help";
    exit;
  },

);


sub np {
  my $what = shift // 'np';

  my $xml_file = get($PLEX_URL);

  my $np = XMLin($xml_file, KeyAttr => { Media => 'id' }, ForceArray => [ 'Media' ]);

  my $now_playing;
  $now_playing->{artist} = $np->{Track}->{grandparentTitle};
  $now_playing->{album}  = $np->{Track}->{parentTitle};
  $now_playing->{title}  = $np->{Track}->{title};
  $now_playing->{year}   = $np->{Track}->{parentYear};

  for my $id(keys(%{ $np->{Track}->{Media} })) {
    $now_playing->{file} = $np->{Track}->{Media}->{$id}->{Part}->{file};
  }

  return
    defined $now_playing->{ $what }
      ? $now_playing->{ $what }
      : sprintf "%s - %s on %s %s",
          $now_playing->{artist},
          $now_playing->{title},
          $now_playing->{album},
          $now_playing->{year} ? "[$now_playing->{year}]" : '';
}
